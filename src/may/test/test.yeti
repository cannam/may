module may.test.test;

import yeti.lang: FailureException;

var goodCompares = 0;

compareUsing comparator obtained expected =
    if comparator obtained expected then
        goodCompares := goodCompares + 1;
        true;
    else
        println "** expected: \(expected)\n   obtained: \(obtained)";
        false;
    fi;

compare obtained expected = compareUsing (==) obtained expected;

time msg f =
   (start = System#currentTimeMillis();
    result = f ();
    end = System#currentTimeMillis();
    println "\(msg): \(end-start)ms";
    result);

select f = fold do r x: if f x then x::r else r fi done [];

failedTests testHash =
    select (!= "")
       (mapHash do name f:
            try
                if f () then "" else
                    println "Test \(name) failed";
                    name;
                fi 
            catch FailureException e:
                println "Test \(name) threw exception: \(e)";
                name;
            yrt;
        done testHash);
        
runTests group testHash =
   (failed = failedTests testHash;
    good = (length testHash - length failed);
    bad = length failed;
    println "\(group): \(good)/\(good+bad) tests passed";
    if not empty? failed then
        println "\(group): Failed tests [\(bad)]: \(strJoin ' ' failed)";
    fi;
    bad);

{
    compare, compareUsing,
    time,
    runTests, 
}

