
module playback;

block = load block;
fr = load framer;
af = load audiofile;
ch = load channels;

import javax.sound.sampled:
    AudioSystem, AudioFormat, AudioFormat$Encoding, SourceDataLine;

import java.nio: ByteBuffer, ByteOrder;

open { rate, channels } = 
   (format = new AudioFormat(AudioFormat$Encoding#PCM_SIGNED, rate, 16,
                             channels, channels * 2, rate, false);
    line = AudioSystem#getSourceDataLine(format);
    line#open(format);
    line#start();
    {
        line,
        channels = line#getFormat()#getChannels(),
        close () = line#close(),
    });

playBlock { line is ~SourceDataLine } b =
   (len = block.length b;
    samples = block.unblock b;
    nb = len * 2;
    bytes = new byte[nb];
    bb = ByteBuffer#wrap(bytes, 0, nb);
    bb#order(ByteOrder#LITTLE_ENDIAN);
    sb = bb#asShortBuffer();
    for [0..len-1] do i: sb#put(i, samples[i] * 32767.0); () done;
    println "writing \(nb) bytes";
    actual = line#write(bytes, 0, nb); 
    ());

play line blocks = for blocks (playBlock line);
    
playStream stream =
   (line = open { rate = stream.sampleRate, channels = stream.channels };
    blocksize = 10240;
    play line (map (ch.mixedFromInterleavedTo line.channels stream.channels)
                   (fr.frames blocksize stream));
    line.close());

playFile filename = playStream (af.open filename);

{
    open, play, playStream, playFile,
}

