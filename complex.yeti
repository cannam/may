
module complex;

import java.lang: ClassCastException;

class Complex(double real, double imag)
    int getReal()
        real,
    int getImag()
        imag,
    double getMagnitude()
        sqrt (real * real + imag * imag),
    double getAngle()
        Math#atan2(imag, real),
    String toString()
        if real == int real and imag == int imag then
            if imag < 0 then
                " \(int real) - \(int (-imag))i"
            else 
                " \(int real) + \(int imag)i"
            fi
        else
            if imag < 0 then
                " \(real) - \((-imag))i"
            else 
                " \(real) + \(imag)i"
            fi
        fi,
    int hashCode()
        Double#valueOf(real)#hashCode() + Double#valueOf(imag)#hashCode(),
    boolean equals(Object other)
        try
            c = other unsafely_as ~Complex;
            c#getReal() == real and c#getImag() == imag
        catch ClassCastException:
            false
        yrt,
end;

typedef opaque cplx = ~Complex;

real c1 is ~Complex -> number =
    c1#getReal();

imaginary c1 is ~Complex -> number =
    c1#getImag();

complex re im is number -> number -> ~Complex =
    new Complex(re, im);

magnitude c is ~Complex -> number =
    c#getMagnitude();

angle c is ~Complex -> number =
    c#getAngle();

add c1 c2 is ~Complex -> ~Complex -> ~Complex =
    complex (real c1 + real c2) (imaginary c1 + imaginary c2);

scale r c is number -> ~Complex -> ~Complex =
    complex (r * real c) (r * imaginary c);

{
   real,
   imaginary,
   complex,
   magnitude,
   angle,
   add,
   scale,
} as {
   real is cplx -> number,
   imaginary is cplx -> number,
   complex is number -> number -> cplx,
   magnitude is cplx -> number,
   angle is cplx -> number,
   add is cplx -> cplx -> cplx,
   scale is number -> cplx -> cplx,
}

