
module yetilab.stream.syntheticstream;

str = load yetilab.stream.stream;
vec = load yetilab.block.fvector;
block = load yetilab.block.block;

generated rate generator =
    // generator takes time in seconds as arg, returns number in -1,+1 range
    str.monoStream {
        var position = 0,
        rate,
        read count = 
           (result = vec.zeros count;
            for [0..count-1] do i:
                result[i] := generator ((position + i) / rate)
            done;
            position := position + count;
            block.block result),
        close = \(),
        };

sinusoid rate freq =
    generated rate (sin . (* (freq / (2*pi * rate))));

whiteNoise rate =
    generated rate \((Math#random() * 2.0) - 1.0);

precalculated rate data is number -> ~double[] -> 'a =
   (n = vec.length data;
    str.monoStream {
        var position = 0,
        len = n,
        rate,
        read count = 
           (rc = min count (len - position);
            result = vec.rangeOf data position rc;
            position := position + rc;
            block.block result),
        close = \(),
    });

{
    generated, precalculated, sinusoid, whiteNoise,
}

