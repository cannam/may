
module framer;

vec = load fvector;
af = load audiofile;

blockList blocksize stream =
    if stream.finished? then
       (stream.close (); [] );
    else
        stream.readMono blocksize :. \(blockList blocksize stream);
    fi;

overlappingBlockList blocksize hopsize stream buffer
    is number -> number -> 'a -> ~double[] -> 'b =
    if stream.finished? then
       (stream.close (); [] ); //!!! wrong! need unit tests! need to read from any stream, not only a file, to facilitate that
    else
        block = stream.readMono hopsize;
        blen = vec.vectorLength block;
        for [0..blen-1] do i: buffer[blocksize - hopsize + i] := block[i] done;
        for [blen..hopsize-1] do i: buffer[blocksize - hopsize + i] := 0.0 done;
        v = vec.subVector buffer 0 blocksize;
        for [hopsize..blocksize-1] do i: buffer[i - hopsize] := buffer[i] done;
        v :. \(overlappingBlockList blocksize hopsize stream buffer);
    fi;

overlappingFrames blocksize hopsize stream =
    overlappingBlockList blocksize hopsize stream (new double[blocksize]);

frames blocksize stream =
    blockList blocksize stream;

overlappingFramesOfFile blocksize hopsize filename =
    overlappingBlockList blocksize hopsize (af.open filename)
        (new double[blocksize]);

framesOfFile blocksize filename =
    blockList blocksize (af.open filename);

{ 
    frames, overlappingFrames,
    framesOfFile, overlappingFramesOfFile,
}

