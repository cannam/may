module may.plot;

vec = load may.vector;
mat = load may.matrix;

{ distinctColour } = load may.plot.colour;
{ log10 } = load may.mathmisc;

import org.jzy3d.plot3d.builder: Mapper;
import org.jzy3d.maths: Range, Coord3d;
import org.jzy3d.chart: Chart, ChartLauncher;
import org.jzy3d.plot3d.builder: Builder;
import org.jzy3d.plot3d.builder.concrete: OrthonormalGrid;
import org.jzy3d.colors.colormaps: ColorMapRainbow;
import org.jzy3d.colors: ColorMapper, Color;
import org.jzy3d.plot3d.rendering.canvas: Quality;
import org.jzy3d.plot3d.rendering.view.modes: ViewPositionMode;
import org.jzy3d.plot3d.primitives: FlatLine2d, Point;
import org.jzy3d.plot3d.primitives.axes: ContourAxeBox;
import org.jzy3d.contour: DefaultContourColoringPolicy;
import org.jzy3d.contour: MapperContourPictureGenerator;
import org.jzy3d.chart.factories.AWTChartComponentFactory;
import org.jzy3d.plot3d.text.drawable: DrawableTextBillboard, DrawableTextBitmap;

import javax.imageio: ImageIO;

import java.io: File;

newMatrixMapper matrix =
   (class MMapper extends Mapper
        double f(double x, double y)
            mat.at matrix y x;
    end;
    new MMapper());

newMatrixLogMapper matrix =
   (class MMapper extends Mapper
        double f(double x, double y)
           (thresh = 1e-100;
            v = (mat.at matrix y x);
            if v < thresh then log10 thresh else log10 v fi);
    end;
    new MMapper());

newMapper mapFunction =
   (class FMapper extends Mapper
        double f(double x, double y)
            mapFunction x y
    end;
    new FMapper());

plotMatrix chart colour matrix is ~Chart -> ~Color -> 'a -> () =
   (mapper = newMatrixMapper matrix;
    size = mat.size matrix;
    //!!! doesn't work if either rows or columns is 1
    xrange = new Range(0, size.columns - 1);
    yrange = new Range(0, size.rows - 1);
    grid = new OrthonormalGrid(xrange, size.columns, yrange, size.rows);
    println "Matrix size: \(size)";
    surface = Builder#buildOrthonormal(grid, mapper); //??? big?
    println "Z Bounds: \(surface#getBounds()#getZmin()) -> \(surface#getBounds()#getZmax())";
    surface#setFaceDisplayed(true);
    surface#setWireframeDisplayed(true);
    surface#setWireframeColor(colour);
    chart#getScene()#getGraph()#add(surface);
    ());

plotContour chart colour matrix is ~Chart -> ~Color -> 'a -> () =
   (mapper = newMatrixMapper matrix;
    size = mat.size matrix;
    //!!! doesn't work if either rows or columns is 1
    xrange = new Range(0, size.columns - 1);
    yrange = new Range(0, size.rows - 1);
//    grid = new OrthonormalGrid(xrange, size.columns, yrange, size.rows);
    eprintln "Matrix size: \(size)";
//    surface = Builder#buildOrthonormal(grid, mapper); //??? big?
//    println "Z Bounds: \(surface#getBounds()#getZmin()) -> \(surface#getBounds()#getZmax())";
    contour = new MapperContourPictureGenerator(mapper, xrange, yrange);
    zmin = mat.minValue matrix;
    zmax = mat.maxValue matrix;
    eprintln "z range: \(zmin) -> \(zmax)";
    cmapper = new ColorMapper(new ColorMapRainbow(), zmin, zmax, new Color(1,1,1,0.5));
    img = contour#getFilledContourImage(new DefaultContourColoringPolicy(cmapper), 4000, 4000, 10);
    outfile = new File("contourplot.png");
\()    ImageIO#write(img, "png", outfile);
    eprintln "wrote to contourplot.png";
//    cab = chart#getView()#getAxe() unsafely_as ~ContourAxeBox;
//    cab#setContourImg(img, xrange, yrange);
    ());

plotCurve chart colour depth curve is ~Chart -> ~Color -> number -> 'a -> () =
   (scene = chart#getScene();
    xx = map (.time) curve;
    yy = map (.value) curve;
    line = new FlatLine2d(xx as ~float[], yy as ~float[], depth);
    line#setWireframeDisplayed(true);
    line#setWireframeColor(colour);
    line#setWireframeWidth(2);
    line#setFaceDisplayed(false);
    scene#add(line);
    chart#getView()#setViewPoint(new Coord3d(0, 0, 0));
/*
    axes = chart#getAxeLayout();
    axes#setXAxeLabelDisplayed(false);
    axes#setYAxeLabelDisplayed(false);
    axes#setZAxeLabelDisplayed(true);
    axes#setZAxeLabel("unit goes here"); //!!!
    axes#setYTickLabelDisplayed(false);
*/
    ());

plotSeries chart colour depth { start, step, values } is ~Chart -> ~Color -> number -> 'a -> () =
   (scene = chart#getScene();
    xx = map do i: start + step * i done [0..length values - 1];
    yy = list values;
    line = new FlatLine2d(xx as ~float[], yy as ~float[], depth);
    line#setWireframeDisplayed(true);
    line#setWireframeColor(colour);
    line#setWireframeWidth(2);
    line#setFaceDisplayed(false);
    scene#add(line);
    chart#getView()#setViewPoint(new Coord3d(0, 0, 0));
/*
    axes = chart#getAxeLayout();
    axes#setXAxeLabelDisplayed(false);
    axes#setYAxeLabelDisplayed(false);
    axes#setZAxeLabelDisplayed(true);
    axes#setZAxeLabel("unit goes here"); //!!!
    axes#setYTickLabelDisplayed(false);
*/
    ());

addCaption chart colour caption is ~Chart -> ~Color -> string -> () =
   (scene = chart#getScene();
    text = new DrawableTextBitmap(caption, new Coord3d(0, 0, 0), colour);
    scene#add(text));

/**
 * Plot a list of typed structures onto a single chart, and return the
 * resulting Chart object.
 *
 * The caller must call dispose() on the Chart when it is no longer
 * needed.
 */
plot structures =
   (quality = Quality#Nicest;
    quality#setAnimated(false);
    chart = AWTChartComponentFactory#chart(quality, "awt");
    var j = 0;
    for structures do s:
        colour = distinctColour j;
        case s of
        Caption caption:
            addCaption chart (new Color(0, 0, 0)) caption;
            j := j - 1; // doesn't count as a distinct structure
        Grid matrix:
            plotMatrix chart colour matrix;
        Contour matrix:
            plotContour chart colour matrix;
        Curve curve:
            plotCurve chart colour j curve;
        Series series:
            plotSeries chart colour j series;
        Vector vector:
            plotSeries chart colour j
                { start = 0, step = 1, values = vec.list vector };
        other:
            failWith "Unable to plot \(other)";
        esac;
        j := j + 1;
    done;
    ChartLauncher#openChart(chart);
    chart);

{
    newMatrixMapper,
    newMatrixLogMapper,
    newMapper,
    plotMatrix, 
    plotCurve,
    plotSeries,
    plot,
}

