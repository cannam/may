
module yetilab.stream.filter;

mat = load yetilab.matrix;
ch = load yetilab.stream.channels;
vec = load yetilab.vector;
bf = load yetilab.vector.blockfuncs;
fr = load yetilab.stream.framer;
win = load yetilab.signal.window;
fft = load yetilab.transform.fft;
syn = load yetilab.stream.syntheticstream;
cplx = load yetilab.complex;

load yetilab.stream.type;
load yetilab.vector.type;
load yetilab.matrix.type;

//!!! todo: synchronized for everything with state assignment

minDurationOf d1 d2 =
    case d1 of 
    Known a:
        case d2 of 
        Known b: Known (min a b);
        Unknown (): Unknown ();
        Infinite (): Known a;
        esac;
    Unknown ():
        case d2 of 
        Known b: Known b;
        Unknown (): Unknown ();
        Infinite (): Unknown ();
        esac;
    Infinite ():
        d2;
    esac;

withDuration nsamples s = //!!! should nsamples be a time in seconds? (no)
   (var pos = 0;
    s with
    {
        get position () = pos,
        get available () = Known (nsamples - pos),
        get finished? () = not (nsamples > pos),
        read count =
           (n = min count (nsamples - pos);
            pos := pos + n;
            if not s.finished? then
                mat.resizedTo { rows = s.channels, columns = n } (s.read n);
            else 
                mat.zeroMatrix { columns = n, rows = s.channels }
            fi),
    });

delayedBy nsamples s = //!!! doc: nsamples may be -ve
   (var prepos = 0;
    zeros n = mat.toRowMajor
       (prepos := prepos + n;
        mat.zeroMatrix { rows = s.channels, columns = n });
    delay = 
        if nsamples < 0 then
            \0 (s.read (-nsamples));
        else
            nsamples
        fi;
    {
        get position () = 
            if prepos < delay then prepos
            elif s.position + nsamples < 0 then 0
            else s.position + nsamples
            fi,
        get channels () = s.channels,
        get sampleRate () = s.sampleRate,
        get available () = 
            case s.available of 
            Known a: Known (a + delay - prepos); 
            other: other 
            esac,
        get finished? () = (prepos >= delay) and s.finished?,
        read count =
            if prepos >= delay then s.read count
            elif prepos + count < delay then zeros count
            else
                nleft = delay - prepos;
                left = zeros nleft;
                right = s.read (count - nleft);
                mat.concat (Horizontal ()) [left, right];
            fi,
        close = s.close
    });

//!!! poor name, confusion with mixed, but consistent with channels.yeti
mixedTo targetChannels s =
    s with
    {
        get channels () = targetChannels,
        read count = ch.mixedTo targetChannels (s.read count),
    };

//!!! what should happen if we mix or multiplex a finite-length and an
//infinite-length stream? or even two streams with differing finite
//lengths? write tests for this. At the moment the resulting stream
//has duration equal to the shortest source stream and finishes as
//soon as any one of them finishes

mixed streams =
   (mix m1 m2 =
       (sz = { rows = max (mat.height m1) (mat.height m2),
               columns = min (mat.width m1) (mat.width m2) };
        if sz.columns == 0 then
            mat.zeroSizeMatrix ()
        else
            mat.sum (mat.resizedTo sz m1) (mat.resizedTo sz m2);
        fi);
    {
        get position () = head (sort (map (.position) streams)),
        get channels () = head (sortBy (>) (map (.channels) streams)),
        get sampleRate () = (head streams).sampleRate, //!!! document this
        get available () =
            fold do dur s: minDurationOf dur s.available done (Infinite ()) streams,
        get finished? () = any id (map (.finished?) streams),
        read count =
           (readTo acc ss count =
               case ss of
               first::rest:
                   part = first.read count;
                   case acc of 
                   Some m:
                       readTo (Some (mix m part)) rest count;
                   None ():
                       readTo (Some part) rest count;
                   esac;
                _: acc;
               esac;
            case readTo (None ()) streams count of
            None (): mat.zeroSizeMatrix ();
            Some m: m;
            esac),
        close () = for streams do s: s.close() done,
    });

multiplexed streams = 
    {
        get position () = head (sort (map (.position) streams)), // can differ after EOS
        get channels () = sum (map (.channels) streams),
        get sampleRate () = (head streams).sampleRate, //!!! document this
        get available () = 
            fold do dur s: minDurationOf dur s.available done (Infinite ()) streams,
        get finished? () = any id (map (.finished?) streams),
        read count =
           (outs = map do s: s.read count done streams;
            minlen = head (sort (map mat.width outs));
            outs = map do m:
                mat.resizedTo { rows = mat.height m, columns = minlen } m
                done outs;
            mat.concat (Vertical ()) outs
            ),
        close () = for streams do s: s.close() done,
    };

repeated s =
    // There is no way to reset a stream (as in principle, they might
    // be "live") so we can't read from the same stream repeatedly --
    // we have to cache its output and then repeat that. This is a
    // little tricky to do efficiently without knowing how long the
    // stream is (in general) or how much is going to be requested at
    // a time.
    if s.available == Infinite () then s
    else
        var pos = 0;
        var cache = mat.zeroSizeMatrix ();
        chunks = array [];
        cachedPartsFor count =
           (start = pos % (mat.width cache);
            avail = (mat.width cache) - start;
            if avail >= count then
                pos := pos + count;
                [mat.columnSlice cache start (start + count)]
            else
                pos := pos + avail;
                mat.columnSlice cache start (start + avail) ::
                    cachedPartsFor (count - avail);
            fi);
        readFromCache count =
           (if (mat.width cache) == 0 then
                cache := mat.concat (Horizontal ()) (list chunks);
                clearArray chunks;
            fi;
            if (mat.width cache) == 0 then
                cache
            else
                mat.concat (Horizontal ()) (cachedPartsFor count);
            fi);
        s with
        {
            get position () = pos,
            get available () = Infinite (),
            get finished? () = false,
            read count =
                if s.finished? then
                    readFromCache count
                else
                    part = s.read count;
                    len = (mat.width part);
                    push chunks part;
                    pos := pos + len;
                    if len == count then part
                    else
                        mat.concat (Horizontal ())
                           [part, readFromCache (count - len)];
                    fi;
                fi,
        }
    fi;

duplicated copies s = 
//!!! doc fact that original s cannot be used independently of this afterward
// (so maybe name is misleading?)
    if copies < 2 then map \s [1..copies];
    else
        pos = [:];
        lowtide () = head (sort (map (at pos) (keys pos)));
        var hightide = 0;
        var cache = mat.zeroSizeMatrix ();
        syncd = synchronized pos;
        advance i n =
           (formerLow = lowtide ();
            pos[i] := pos[i] + n;
            encroachment = lowtide () - formerLow;
            if encroachment > 0 then
                cache := mat.columnSlice cache encroachment (mat.width cache);
            fi);
        map do instance:
            pos[instance] := 0;
            {
                get position () = syncd \(pos[instance]),
                get channels () = syncd \(s.channels),
                get sampleRate () = syncd \(s.sampleRate),
                get available () = syncd
                  \(case s.available of
                    Known av: Known (av + (hightide - pos[instance]));
                    other: other;
                    esac),
                get finished? () = syncd
                  \(if not s.finished? then false
                    else pos[instance] >= hightide
                    fi),
                read count = syncd
                  \(ready = hightide - pos[instance];
                    if s.finished? and ready <= 0 
                    then mat.zeroSizeMatrix ()
                    else
                        if count > ready then
                            more = s.read (count - ready);
                            cache := mat.concat (Horizontal ()) [cache, more];
                            hightide := hightide + (mat.width more);
                        fi;
                        offset = pos[instance] - (lowtide ());
                        chunk = mat.columnSlice cache offset (offset + count);
                        advance instance (mat.width chunk);
                        chunk;
                    fi),
                close () = syncd
                  \(delete pos instance;
                    if empty? pos then
                        s.close ()
                    fi),
            }
            done [1..copies];
    fi;

zeroPaddedFreqFrames framesize =
//!!! mono only for now
   (f = fft.realForward (framesize * 2);
    do stream:
        padded = 
           (map (vec.resizedTo (framesize * 2))
               (fr.monoFrames { framesize, hop = framesize } stream));
        map f padded;
    done);

time f =
   (start = System#currentTimeMillis();
    result = f ();
    end = System#currentTimeMillis();
    println "\(end-start)ms";
    result);

fastConvolve irframes history remaining acc = 
   (case remaining of
    first::rest:
        newhistory = first::history;
        convolvedFrame = 
           (fold do cc1 cc2: map2 cplx.add cc1 cc2; done
               (list (cplx.zeros (length (head irframes))))
               (map2 do irf histf:
                    map2 cplx.multiply irf histf;
                    done irframes newhistory));
        fastConvolve irframes newhistory rest (acc ++ [convolvedFrame]));
     _: acc;
    esac);

fastConvolvedWith ir framesize s =
   (framer = zeroPaddedFreqFrames framesize;
    irfr = framer (syn.precalculatedMono (s.sampleRate) ir);
len = time \(length irfr);
println "irfr has \(len) frames";
    sigfr = framer s;
len = time \(length sigfr);
println "sigfr has \(len) frames";
    fs1 = framesize + 1;
    cframes = fastConvolve
        irfr [] (sigfr ++ (map \(cplx.zeros fs1) [1..length irfr-1])) [];
    rframes = (vec.zeros (framesize * 2)) ::
        map (fft.realInverse (framesize * 2)) cframes;
len = time \(length cframes);
println "cframes has \(len) frames";
len = time \(length rframes);
println "rframes has \(len) frames";
    fr.streamOverlapping s.sampleRate
       { framesize = framesize * 2, hop = framesize, window = win.boxcar }
       (map mat.newRowVector rframes));

plainConvolvedWith ir s =
   (if mat.height ir != s.channels then
        failWith "Signal stream and IR must have same number of channels (\(s.channels) != \(mat.height ir))"
    fi;
    var history = mat.toRowMajor
       (mat.zeroMatrix { rows = s.channels, columns = mat.width ir - 1 });
    s with 
    {
        get finished? () =
            s.finished? and (mat.empty? history),
        get available () = 
            case s.available of
            Known n: Known (n + mat.width history);
            other: other;
            esac,
        read count = 
           (// Example: The IR is four samples long; we have three
            // samples in history; two samples are available to read
            // before the stream runs out. That means we can return
            // up to five samples. Caller requests 6.
            signal = s.read count;                     // -> two samples
            siglen = mat.width signal;                 // -> 2
            histlen = mat.width history;               // -> 3
            convlen = min count (siglen + histlen);    // -> 5
            input = mat.resizedTo { rows = s.channels, columns = convlen }
                signal;  // example input now 5 samples, of which 2 are signal
            output = array (map \(new double[convlen]) [1..s.channels]);
            for [0..s.channels - 1] do ch:
                for [0..mat.width input - 1] do i:
                    for [0..mat.width ir - 1] do j:
                        v = 
                            if i >= j then
                                mat.at input ch (i - j)
                            else
                                ix = mat.width ir + i - j - 1;
                                if ix >= histlen then 
                                    0 
                                else
                                    mat.at history ch ix
                                fi
                            fi;
                        output[ch][i] := output[ch][i] + v * (mat.at ir ch j);
                    done;
                done;
            done;
            // Remove from history a number of samples equal to the
            // number returned; add to it a number equal to the number
            // read from source
            extended = mat.concat (Horizontal ()) [history, signal]; // -> 5
            newlen = (histlen + siglen) - convlen; // -> 0
            w = mat.width extended;
            history := mat.columnSlice extended (w - newlen) w;
            mat.newMatrix (RowMajor ()) (map vec.vector output)),
    });

nextPowerOfTwo n =
   (nextPowerOfTwo' p n =
        if p >= n then p
        else nextPowerOfTwo' (p * 2) n
        fi;
    nextPowerOfTwo' 1 n);

//!!! todo: partial application so as to preprocess ir (in fast convolution case)
convolvedWith options ir s = //!!! cheap mono thing here
   (var type = Fast ();
    var framesize = nextPowerOfTwo (mat.width ir);
    for options \case of
    //!!! todo: test these options
        Fast s: if s then type := Fast () else type := Plain () fi;
        Framesize n: framesize := nextPowerOfTwo n;
        esac;
    case type of
    Fast ():
        fastConvolvedWith (mat.getRow 0 ir) framesize s;
    Plain ():
        plainConvolvedWith ir s;
    esac);

{
    withDuration is number -> stream -> stream,
    delayedBy is number -> stream -> stream,
    mixedTo is number -> stream -> stream,
    mixed is list<stream> -> stream,
    multiplexed is list<stream> -> stream,
    repeated is stream -> stream,
    duplicated is number -> stream -> list<stream>,
    convolvedWith is list<Fast boolean | Framesize number> -> matrix -> stream -> stream,
}

