
module framer;

/**
 * Framer expresses a stream (or a file) as a lazy list of (possibly
 * overlapping) frames of mono data.
 */

vec = load fvector;
block = load block;
af = load audiofile;

blockList blocksize stream =
    if stream.finished? then
       (stream.close (); [] );
    else
        block.resizedTo blocksize (stream.readMono blocksize)
            :. \(blockList blocksize stream);
    fi;

overlappingBlockList bs hop stream valid buffer
    is number -> number -> 'a -> number -> ~double[] -> 'b =
   (
    b = stream.readMono hop;
    obtained = block.length b;
    samples = block.unblock b;

    // Retain blocksize - hop samples from old buffer, add hop samples
    // (zero-padded if necessary) just read
    buffer = vec.concat
       (vec.rangeOf buffer hop (bs-hop))
       (vec.resizedTo hop samples);

    // Number of "valid" elements (not tail-end zero-padding) left in buffer
    remaining = valid - (hop - obtained);

    if remaining <= 0 then
        stream.close ();
        [];
    else
        v = block.block buffer;
        v :. \(overlappingBlockList bs hop stream remaining buffer);
    fi);

frames { blocksize, hop } stream =
    if blocksize == hop then
        blockList blocksize stream
    else
        overlappingBlockList blocksize hop stream 
            blocksize (vec.zeros blocksize);
    fi;

framesOfFile { blocksize, hop } filename =
    if blocksize == hop then
        blockList blocksize (af.open filename)
    else
        overlappingBlockList blocksize hop (af.open filename)
            blocksize (vec.zeros blocksize);
    fi;

{ 
    frames,
    framesOfFile,
}

