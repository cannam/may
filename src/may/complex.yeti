
module may.complex;

load may.vector.type;
load may.complex.type;

vec = load may.vector;

import java.lang: ClassCastException;

class Cplx(double real, double imag)
    double getReal()
        real,
    double getImag()
        imag,
    double getMagnitude()
        sqrt (real * real + imag * imag),
    double getAngle()
        Math#atan2(imag, real),
    String toString()
        if real == int real and imag == int imag then
            if imag < 0 then
                " \(int real) - \(int (-imag))i"
            else 
                " \(int real) + \(int imag)i"
            fi
        else
            if imag < 0 then
                " \(real) - \((-imag))i"
            else 
                " \(real) + \(imag)i"
            fi
        fi,
    int hashCode()
        Double#valueOf(real)#hashCode() + Double#valueOf(imag)#hashCode(),
    boolean equals(Object other)
        try
            c = other unsafely_as ~Cplx;
            c#getReal() == real and c#getImag() == imag
        catch ClassCastException:
            false
        yrt,
end;

real c1 is ~Cplx -> number =
    c1#getReal();

imaginary c1 is ~Cplx -> number =
    c1#getImag();

complex re im is number -> number -> ~Cplx =
    new Cplx(re, im);

magnitude c is ~Cplx -> number =
    c#getMagnitude();

angle c is ~Cplx -> number =
    c#getAngle();

sum' cc is list?<~Cplx> -> ~Cplx =
    complex (sum (map real cc)) (sum (map imaginary cc));

add c1 c2 is ~Cplx -> ~Cplx -> ~Cplx =
    complex (real c1 + real c2) (imaginary c1 + imaginary c2);

multiply c1 c2 is ~Cplx -> ~Cplx -> ~Cplx =
   (a = real c1;
    b = imaginary c1;
    c = real c2;
    d = imaginary c2;
    complex (a * c - b * d) (b * c + a * d)); //!!! needs unit tests

scale r c is number -> ~Cplx -> ~Cplx =
    complex (r * real c) (r * imaginary c);

zero = complex 0 0;

zeros n is number -> array<~Cplx> =
    array (map \(complex 0 0) [1..n]);

magnitudes cc is list?<~Cplx> -> vector =
    vec.fromList (map magnitude cc);

angles cc is list?<~Cplx> -> vector =
    vec.fromList (map angle cc);

complexArray reals imags =
    array (map2 complex (vec.list reals) (vec.list imags));

{
   real,
   imaginary,
   complex,
   magnitude,
   angle,
   sum = sum',
   add,
   multiply,
   scale,
   zero,
   zeros,
   magnitudes,
   angles,
   complexArray
} as {
   real is cplx -> number,
   imaginary is cplx -> number,
   complex is number -> number -> cplx,
   magnitude is cplx -> number,
   angle is cplx -> number,
   sum is list?<cplx> -> cplx,
   add is cplx -> cplx -> cplx,
   multiply is cplx -> cplx -> cplx,
   scale is number -> cplx -> cplx,
   zero is cplx,
   zeros is number -> array<cplx>,
   magnitudes is list?<cplx> -> vector,
   angles is list?<cplx> -> vector,
   complexArray is vector -> vector -> array<cplx>
}

